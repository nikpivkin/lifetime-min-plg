import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.3'
    id 'xyz.jpenilla.run-paper' version '2.0.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
}

group = 'io.github.nikpivkin'
version = '0.0.1'

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT"
    compileOnly "io.insert-koin:koin-core:3.3.3"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = targetJavaVersion.toString()
    }
}

bukkit {
    name = "lifetime-minecraft-plugin"
    author = "Nikita Pivkin"
    main = 'io.github.nikpivkin.lifetime.LifetimePlugin'
    description = project.description as String
    version = project.version as String

    apiVersion = '1.19'

    commands {
        lifetime {}
    }
}

tasks {
    runServer {
        minecraftVersion("1.19.2")
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}